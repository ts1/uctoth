#!/usr/bin/env coffee

fs = require 'fs'
{ Board, BLACK, pos_to_str } = require './board'
{ readlines, int } = require './util'
make_player = require './player'
uct = require './uct'
pattern_eval = require('./pattern_eval')('scores')

FILENAME = 'games'
RANDOM = .15
WLD = 18
FULL = 16
BALANCE_MAX = Infinity

player = make_player
  book: null
  strategy: uct
    evaluate: pattern_eval
    verbose: false
    random: RANDOM
  verbose: false
  solve_wld: WLD
  solve_full: FULL

read_games = ->
  games = {}
  balance = 0
  await readlines FILENAME, (line) ->
    [moves, score] = line.split(' ')
    games[moves] = true
    balance += int(score)
  {games, balance}

play = (player) ->
  board = new Board
  turn = BLACK
  moves = ''
  loop
    unless board.any_moves turn
      turn = -turn
      unless board.any_moves turn
        break
    {move} = await player board, turn
    flips = board.move turn, move
    console.assert flips.length
    m = pos_to_str(move, turn)
    moves += m
    process.stdout.write m
    turn = -turn
  outcome = board.outcome()
  process.stdout.write " #{outcome}\n"
  {moves, outcome}

do ->
  args = process.argv.slice 2
  if args.length == 1
    max_games = parseInt args[0]
  else
    max_games = Infinity

  if fs.existsSync FILENAME
    {games, balance} = await read_games()
  else
    games = {}
    balance = 0
  n_games = Object.keys(games).length
  console.log "#{n_games} games loaded"
  console.log "balance #{balance}"
  n_dup = 0
  n_play = 0
  n_skip = 0
  while n_games < max_games
    if fs.existsSync('.reload-selfplay')
      break
    {moves, outcome} = await play player
    n_play++
    if games[moves]
      n_dup++
      console.log "DUPLICATED #{n_dup}/#{n_play}"
      continue
    b = balance + outcome
    if b > BALANCE_MAX or b < -BALANCE_MAX
      console.log "SKIP FOR BALANCE (current #{balance})"
      n_skip++
      continue
    balance += outcome
    games[moves] = true
    fs.appendFileSync FILENAME, "#{moves} #{outcome}\n"
    n_games++
  console.log "played #{n_play} duplicated #{n_dup} skipped #{n_skip}"
